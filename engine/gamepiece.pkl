/// gamepiece is the superclass for units, buildings, etc, and represents an on-screen, in-game object. 
abstract module gamepiece 
extends "data.pkl"
import "data.pkl"
import "ability/attack.pkl" as Attack
import "ability/heal.pkl" as Heal
import "ability/spell.pkl" as Spell
import "ability/passive.pkl" as Passive
import "turret/turret.pkl" as Turret
import "types.pkl" as $t
import "gamepiece.pkl" as me
local methis = this

/// short description of the purpose of this, to be used in summaries in UI. Many of these won't be in game. 
/// examples: "fast melee attacker" for a unit, "vision buff" for an upgrade, etc
flavor: String?

/// hexite cost to build. 0 for gamepieces that are cast
hexite_cost: Number? 
flux_cost: Number?
build_count: Number? = 1
cooldown: Number?
energy_cost: Number?
topbar_cost: Number?
build_time: Number?
rebuildable: Boolean?
rebuild_time: Number?
domain: $t.DomainType = "ground"

hotkey: $t.HotKey?
tier: $t.Tier?

gathers_flux: Number?
gathers_rich_flux: Number?

gathers_hexite: Number?
gathers_empty_hexite: Number?

supply: Number?
hp: Number?
vision: Number?
speed: Number?
shields: Number?
abes_energy: Number?
energy: Number?
armor: Number?

armor_type: $t.ArmorType = "none"
stun_resist: Number?

provides_flux: Number?
provides_hex: Number?
provides_supply: Number?
provides_biomass: Number?
provides_detection: Number?
provides_upgrades_for: Listing<String>(isDistinct)?

upgraded_by: Listing<String>(isDistinct)?

transforms_from: Listing<String>(isDistinct)?
transforms_into: Listing<String>(isDistinct)?

max_turrets: Number?
turret_type: "turret" | "sacrifice"? =
  if (turrets.length == 0) null 
  else throw("turret_type must be set if turrets is used.")

//@TODO: should refactor the slug logic into a function but not now
attacks: Listing<Attack> = new Listing<Attack> {
  default { 
    faction = methis.faction 
    ability_of = methis.slug
    slug = $t.makeRecursiveSlug(this.name, ability_of, "attack")
  }
}

heals: Listing<Heal> = new Listing<Heal> {
  default { 
    faction = methis.faction 
    ability_of = methis.slug
    slug = $t.makeRecursiveSlug(this.name, ability_of, "heal")
  }
}

spells: Listing<Spell> = new Listing<Spell> {
  default { 
    faction = methis.faction 
    ability_of = methis.slug
    slug = $t.makeRecursiveSlug(this.name, ability_of, "spell")
  }
}

passives: Listing<Passive> = new Listing<Passive> {
  default { 
    faction = methis.faction 
    ability_of = methis.slug
    slug = $t.makeRecursiveSlug(this.name, ability_of, "passive")
  }
}

turrets: Listing<Turret> = new Listing<Turret> {
  default { 
    faction = methis.faction 
    turret_of = methis.slug
    slug = $t.makeRecursiveSlug(this.name, turret_of, "turret")
  }
}

hidden fixed children = attacks.toList() + heals.toList() + spells.toList() + passives.toList() + turrets.toList()
hidden fixed recursive_children = children + children.flatMap((x) -> x.recursive_children)
