open module ability
extends "child.pkl"
import "types.pkl" as $t
import "ability.pkl" as me
local methis = this

activationType: "auto" | "activated" | "passive" | "triggered" =
  if (subtype == "attack") "auto"
  else if (subtype == "heal") "auto"
  else if (subtype == "faction-passive") "passive"
  else if (subtype == "faction-talent") "passive"
  else if (subtype == "death-triggered") "triggered"
  else "activated"

fixed type: String = "ability"
fixed subtype: String = fromPath.abilityType ?? abilityType
hidden abilityType: "attack" | "heal" | "spell" | "passive" | "toggle" | "recharge-energy" | "death-triggered";

name: String =
  if (subtype == "attack") "Attack"
  else if (subtype == "heal") "Heal"
  else if (subtype == "passive") "Passive"
  else null

hotkey: $t.HotKey?
reverseHotKey: $t.HotKey?
targets: Listing<$t.TargetType>
targetMode: $t.TargetMode?
requiresMode: String?
togglesMode: ("siege" | "attack")?
energyCost: Number?
energyType: $t.EnergyType?
/// also called reloadTime
cooldown: Number?
cooldownAtBuild: Boolean?
duration: Number?
range: Number?
/// also called damage point, attack delay, or cast point
delay: Number?
abilityOf: String? = fromPath.abilityOf ?? parent.slug
// abilityOf: String? = fromPath.abilityOf ?? "WHAT: \(name)"

shots: Number?
volleys: Number?

teleport: Number?
providesDetection: Number?

local function xPerSec(val: Number?, numShots: Number?, numVolleys: Number?, cd: Number?) =
  if (val == null) null else
  if (cd == null) null else
  let (amt = val)
  let (s = (numShots ?? 1))
  let (vo = (numVolleys ?? 1))
  ((amt * s * vo) / cd).round().toInt()

effectArea: Number?
arcAngle: Number?
chargeTime: Number?
chargeMaxMultiplier: Number?

damage: Number?
damageOverTime: Number? // dps will have to change based on this as well
damagePerSec: Number? =
  if (damageOverTime != null) null
  else xPerSec(damage, shots, volleys, cooldown)
damageDisplay: String? = if (damage == null) null else
  "\(damage)"
  + (if ((shots ?? 1) > 1) " x\(shots)" else "")
  + (if ((volleys ?? 1) > 1) " x\(volleys)" else "")

healing: Number?
healingOverTime: Number?
healingPerSec: Number? =
  if (healingOverTime != null) null
  else xPerSec(healing, shots, volleys, cooldown)
healingDisplay: String? = if (healing == null) null else
  "\(healing)"
  + (if ((shots ?? 1) > 1) " x\(shots)" else "")
  + (if ((volleys ?? 1) > 1) " x\(volleys)" else "")

energyRecharge: Number?
energyRechargeOverTime: Number?
energyRechargePerSec: Number? =
  if (energyRechargeOverTime != null) null
  else xPerSec(energyRecharge, shots, volleys, cooldown)
energyRechargeDisplay: String? = if (energyRecharge == null) null else
  "\(energyRecharge)"
  + (if ((shots ?? 1) > 1) " x\(shots)" else "")
  + (if ((volleys ?? 1) > 1) " x\(volleys)" else "")

areaRange: Number?
areaDamage: Number?
areaOverTime: Number?

chainTargets: Number?
chainReductionPercent: Number?

// like area, but splash should only be attached to a direct dmg amount
splashPercent: Number?
splashRange: Number?
splashOverTime: Number?
splashDisplay: String? =
  if (splashPercent == null || splashRange == null)
    null
  else
    "\(splashPercent)% over \(splashRange) range"

bonusPercent: Number?
bonusVsTags: Listing<String>?

requiresAddOn: String?

autocast: "always" | "toggle" | "never" =
  if (activationType == "auto") "always" else "never"
