module turret
extends "child.pkl"
import "ability.pkl" as Ability
import "types.pkl" as $t
import "turret.pkl" as me
local methis = this

fixed type = "turret"
fixed subtype: "turret" | "sacrifice" 

extend_tags { 
  when (subtype == "turret") { "turret" } 
  when (subtype == "sacrifice") { "bonus:sacrifice" }
}
turret_of: String 

local parent_info: Ability.ParentInfo = new {
  id = methis.id
  slug = methis.slug
  src = methis.src
  from_path = methis.parent.from_path
}

attacks: Mapping<String, Ability> = new Mapping<String, Ability> {
  default { key -> name = $t.makeSlug(key); parent = parent_info; ability_type = "attack" }
}

heals: Mapping<String, Ability> = new Mapping<String, Ability> {
  default { key -> name = $t.makeSlug(key); parent = parent_info; ability_type = "heal" }
}

spells: Mapping<String, Ability> = new Mapping<String, Ability> {
  default { key -> name = $t.makeSlug(key); parent = parent_info; ability_type = "spell" }
}

passives: Mapping<String, Ability> = new Mapping<String, Ability> {
  default { key -> name = $t.makeSlug(key); parent = parent_info; ability_type = "passive" }
}

nested_mappings { 
  "attacks"
  "heals"
  "spells"
  "passives"
}

hidden fixed children = List(attacks, heals, spells, passives).flatMap((x) -> x.toMap().values.toList())
